/*=========================================================
Instant Third-Class Jobs
by Mumbles
===========================================================
Description:
Allows players to select a free maxed out third-class job
upon initial login.
===========================================================
Compatibility:
Optimised for Hercules emulators.
===========================================================
Changelog:
v1.0 - First version.
	v1.0.1 - Fixed typo.
v2.0 - Optimised for Hercules emulators.
	v2.0.1 - Compressed script.
v2.1 - A little housekeeping.
	v2.1.1 - Minor change to jobchange method.
	v2.1.2 - Fixed issue with arrays.
	v2.1.3 - Updated version format.

v3.0(Unofficial version)
Customization Details by Happy
I modified the script the script a little.

+auto reset, baselevel check, novice check
reason: If you skip the job change you can get some skillpoints
then you can relog and job change, obtaining more skill points
than you should get, this would be bad for servers that allows
few skill levels only eg. 60-70 max.

+ability to skip the instant change for good and some dialogue.
reason: added in-case the player doesn't want to be a third job.

+ability to add quest skills automatically
Thanks to Platinum Skill npc by Keichii and Darkchild
=========================================================*/

-	script	thirds	-1,{

	/*-----------------------------------------------------
	Function: colour()
	-------------------------------------------------------
	Description: Colours the defined string.
	-------------------------------------------------------
	Usage:
	colour("<colour hex>", "<string>");
	-----------------------------------------------------*/
	function colour {
		return "^"+ getarg(0) + getarg(1) +"^000000";
	}
	
	
	/*-----------------------------------------------------
	Configuration
	-----------------------------------------------------*/
	OnInit:
		.trans = 1;		// Transcend? 0 = no, 1 = yes (default: 1)
		
		.auto_quest = 1; 	// Auto add quest skills 0 = no, 1 = yes (default: 1)

		.auto_reset = 1; 	// Auto reset stats for security 0 = no, 1 = yes (default: 1)
		
		// Job names
		setarray .job_name$[0],	"Rune Knight", "Warlock", "Arch Bishop", "Ranger", "Mechanic", "Guillotine Cross",
								"Royal Guard", "Sorcerer", "Sura", "Minstrel/Wanderer", "Genetic", "Shadow Chaser";
		
		// Job IDs
		setarray .job_id[0],	Job_Rune_Knight, Job_Warlock, Job_Arch_Bishop, Job_Ranger, Job_Mechanic, Job_Guillotine_Cross,
								Job_Royal_Guard, Job_Sorcerer, Job_Sura, Job_Minstrel, Job_Genetic, Job_Shadow_Chaser;
		
		// Job IDs (Trans)					 
		setarray .job_id_t[0],	Job_Rune_Knight_T, Job_Warlock_T, Job_Arch_Bishop_T, Job_Ranger_T, Job_Mechanic_T, Job_Guillotine_Cross_T,
								Job_Royal_Guard_T, Job_Sorcerer_T, Job_Sura_T, Job_Minstrel_T, Job_Genetic_T, Job_Shadow_Chaser_T;
		end;
	
	
	/*-----------------------------------------------------
	Script
	-----------------------------------------------------*/
	OnPCLoginEvent:
		if(auto_third || BaseLevel > 1 || Class != Job_Novice) {
			end;
		}
			
		while (true) {
			mes "[Instant Job Changer]";
			mes "I can make you a third job and max your levels, do you want it?";
			mes "Once you skip this, you'll have to create a new char to see me again.";
			mes "If you don't want to be a third job, choose skip, but you'll have to level on your own";
			if (select("I want it","Skip") == 2) {
				next;
				mes "You opt to skip the instant job";
				auto_third = 1;
				close;
			}
			mes "Please choose a Third Job";
			menu implode(.job_name$, ":"), -;
			
			.@index = @menu - 1;
			
			switch(select("Confirm - ["+ colour("008800", .job_name$[.@index]) +"]:Change selection:"+ colour("FF0000", "End session"))) {
				case 1:
					jobchange (.trans ? .job_id_t[.@index] : .job_id[.@index]);
					
					/*-----------------------------------------------------
					Job Change Settings
					-----------------------------------------------------*/
					BaseLevel = 150;	// Max base level
					JobLevel = 70;		// Max job level
					
					skill NV_BASIC, 9, 0;	// Level 9 Basic Skill
					SkillPoint = 187;		// 49 First, 69 Trans, 69 Third
					/*---------------------------------------------------*/
					
					if (.trans) {
						StatusPoint += 100;
					}
					
					if (.auto_reset) {
						resetskill;
						resetstatus;
					}
					
					if (.auto_quest) {
						skill 142,1,0;
						switch(BaseClass) {
							case 0: if (Class !=23) skill 143,1,0; break;
							case 1: skill 144,1,0; skill 145,1,0; skill 146,1,0; break;
							case 2: skill 157,1,0; break;
							case 3: skill 147,1,0; skill 148,1,0; break;
							case 4: skill 156,1,0; break;
							case 5: skill 153,1,0; skill 154,1,0; skill 155,1,0; break;
							case 6: skill 149,1,0; skill 150,1,0; skill 151,1,0; skill 152,1,0; break;
							default: break;
						}
						switch(BaseJob) {
							case 7: skill 1001,1,0; break;
							case 8: skill 1014,1,0; break;
							case 9: skill 1006,1,0; break;
							case 10: skill 1012,1,0; skill 1013,1,0; break;
							case 11: skill 1009,1,0; break;
							case 12: skill 1003,1,0; skill 1004,1,0; break;
							case 14: skill 1002,1,0; break;
							case 15: skill 1015,1,0; skill 1016,1,0; break;
							case 16: skill 1007,1,0; skill 1008,1,0; skill 1017,1,0; skill 1018,1,0; skill 1019,1,0; break;
							case 17: skill 1005,1,0; break;
							case 18: skill 238,1,0; break;
							case 19: skill 1010,1,0; break;
							case 20: skill 1011,1,0; break;
							default: break;
						}				
					}

					auto_third = 1;
					close;
				
				case 2:
					break;
					
				case 3:
					close;
			}
		}
		
}
